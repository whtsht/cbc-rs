LINE_COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }
BLOCK_COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
SP = _{ " " | "\t" }

// Keywords

VOID       = { "void" }
CHAR       = { "char" }
SHORT      = { "short" }
INT        = { "int" }
LONG       = { "long" }
STRUCT     = { "struct" }
UNION      = { "union" }
ENUM       = { "enum" }
STATIC     = { "static" }
EXTERN     = { "extern" }
CONST      = { "const" }
SIGNED     = { "signed" }
UNSIGNED   = { "unsigned" }
IF         = { "if" }
ELSE       = { "else" }
SWITCH     = { "switch" }
CASE       = { "case" }
DEFAULT    = { "default" }
WHILE      = { "while" }
DO         = { "do" }
FOR        = { "for" }
RETURN     = { "return" }
BREAK      = { "break" }
CONTINUE   = { "continue" }
GOTO       = { "goto" }
TYPEDEF    = { "typedef" }
IMPORT     = { "import" }
SIZEOF     = { "sizeof" }

KEYWORD = _{
    (
      VOID
    | CHAR
    | SHORT
    | INT
    | LONG
    | STRUCT
    | UNION
    | ENUM
    | STATIC
    | EXTERN
    | CONST
    | SIGNED
    | UNSIGNED
    | IF
    | ELSE
    | SWITCH
    | CASE
    | DEFAULT
    | WHILE
    | DO
    | FOR
    | RETURN
    | BREAK
    | CONTINUE
    | GOTO
    | TYPEDEF
    | IMPORT
    | SIZEOF
    ) ~
    !ANY
}

// Punctuation

PLUS      = { "+" }
MINUS     = { "-" }
STAR      = { "*" }
SLASH     = { "/" }
PERCENT   = { "%" }
CARET     = { "^" }
NOT       = { "!" }
AND       = { "&" }
OR        = { "|" }
TILDE     = { "~" }
AAND      = { "&&" }
OOR       = { "||" }
SHL       = { "<<" }
SHR       = { ">>" }
PLUSEQ    = { "+=" }
MINUSEQ   = { "-=" }
STAREQ    = { "*=" }
SLASHEQ   = { "/=" }
PERCENTEQ = { "%=" }
CARETEQ   = { "^=" }
ANDEQ     = { "&=" }
OREQ      = { "|=" }
SHLEQ     = { "<<=" }
RHLEQ     = { ">>=" }
EQ        = { "=" }
EEQ       = { "==" }
NE        = { "!=" }
GT        = { ">" }
LT        = { "<" }
GE        = { ">=" }
LE        = { "<=" }
QUESTION  = { "?" }
PPLUS     = { "++" }
MMINUS    = { "--" }
DOT       = { "." }
DDDOT     = { "..." }
ARROW     = { "->" }
COLON     = { ":" }
SCOLON    = { ";" }

// Left Square brackets
LSB       = { "[" }
// Right Square brackets
RSB       = { "]" }
// Left Parentheses
LPT       = { "(" }
// Right Parentheses
RPT       = { ")" }
// Left Curly braces
LCB       = { "{" }
// Right Curly braces
RCB       = { "}" }


INTEGER = @{
      '1'..'9' ~ ('0'..'9')* ~ "U"? ~ "L"?
    | "0" ~ "x" ~ ('0'..'9' | 'a'..'f' | 'A'..'F')+ ~ "U"? ~ "L"?
    | "0" ~ "o" ~ ('0'..'7')+ ~ "U"? ~ "L"?
    | "0" ~ "b" ~ ("0" | "1")+ ~ "U"? ~ "L"?
    | "0" ~ "U"? ~ "L"?
}

IDENTIFIER = @{
    !KEYWORD ~ (('a'..'z' | 'A'..'Z' | "_") ~ ('a'..'z' | 'A'..'Z' | "_" | '0'..'9')*)
}

TYPEREF_BASE = {
     VOID
   | CHAR
   | SHORT
   | INT
   | LONG
   | UNSIGNED ~ CHAR
   | UNSIGNED ~ SHORT
   | UNSIGNED ~ INT
   | UNSIGNED ~ LONG
   | STRUCT ~ IDENTIFIER
   | UNION ~ IDENTIFIER
   | IDENTIFIER
}

NAME = { IDENTIFIER }

TYPEREF = { TYPEREF_BASE ~ (
      LSB ~ RSB
    | LSB ~ INTEGER ~ RSB
    | STAR
    | LPT ~ PARAMS ~ RPT
    )*
}

TYPE = _{ TYPEREF }

PARAM = {
    TYPE ~ NAME
}

FIXED_PARAMS = {
    PARAM ~ ("," ~ PARAM)*
}

VAR_PARAMS = { "," ~ "..." }

PARAMS = {
      FIXED_PARAMS ~ VAR_PARAMS?
    | VOID
}

CHARACTER = { "'" ~ ASCII_ALPHA ~ "'" }

STRING = { "\"" ~ ASCII_ALPHA* ~ "\""}

PRIMARY = {
      INTEGER
    | CHARACTER
    | STRING
    | IDENTIFIER
    | "(" ~ EXPR ~ ")"
}

UNARY = {
      (PPLUS | MMINUS) ~ UNARY
    | (PLUS | MINUS | NOT | TILDE | STAR | AND) ~ TERM
    | SIZEOF ~ LPT ~ TYPE ~ RPT
    | SIZEOF ~ UNARY
    | POSTFIX
}

POSTFIX = {
      PRIMARY ~ (
        PPLUS
      | MMINUS
      | DOT ~ NAME
      | ARROW ~ NAME
      | LSB ~ EXPR ~ RSB
      | LPT ~ ARGS ~ RPT
      )*
}

ARGS = {
    EXPR ~ ("," ~ EXPR)*
}

TERM = {
    LPT ~ TYPE ~ RPT ~ TERM | UNARY
}

OPASSIGN_OP = {
      "+="
    | "-="
    | "*="
    | "/="
    | "%="
    | "&="
    | "|="
    | "^="
    | "<<="
    | ">>="
}

EXPR = {
      TERM ~ EQ ~ EXPR10
    | TERM ~ OPASSIGN_OP ~ EXPR10
    | EXPR10
}

EXPR10 = {
    EXPR9 ~ (QUESTION ~ EXPR10 ~ COLON ~ EXPR10)?
}

EXPR9 = {
    EXPR8 ~ (OOR ~ EXPR8)*
}

EXPR8 = {
    EXPR7 ~ (AAND ~ EXPR7)*
}

EXPR7 = {
    EXPR6 ~ ((GE | LE | GT | LT | EEQ | NE) ~ EXPR6)*
}

EXPR6 = {
    EXPR5 ~ (OR ~ EXPR5)*
}

EXPR5 = {
    EXPR4 ~ (CARET ~ EXPR4)*
}

EXPR4 = {
    EXPR3 ~ (AND ~ EXPR3)*
}

EXPR3 = {
    EXPR2 ~ ((SHL | SHR) ~ EXPR2)*
}

EXPR2 = {
    EXPR1 ~ ((PLUS | MINUS) ~ EXPR1)*
}

EXPR1 = {
    TERM ~ ((STAR | SLASH | PERCENT) ~ TERM)*
}

STMTS = { STMT* }

STMT = {
      SCOLON
    | EXPR ~ SCOLON
    | BLOCK
    | IF_STMT
    | WHILE_STMT
    | DOWHILE_STMT
    | FOR_STMT
    | SWITCH_STMT
    | BREAK_STMT
    | CONTINUE_STMT
    | GOTO_STMT
    | RETURN_STMT
    | DEF_VARS
}

BLOCK = {
    "{" ~ STMTS ~ "}"
}

IF_STMT = {
    IF ~ "(" ~ EXPR ~ ")" ~ STMT ~ ELSE ~ STMT
}

WHILE_STMT = {
    WHILE ~ "(" ~ EXPR ~ ")" ~ STMT
}

DOWHILE_STMT = {
    DO ~ STMT ~ WHILE ~ "(" ~ EXPR ~ ")" ~ SCOLON
}

FOR_STMT = {
    FOR ~ "(" ~ EXPR ~ SCOLON ~ EXPR ~ SCOLON ~ EXPR ~ ")" ~ STMT
}

SWITCH_STMT = {
    SWITCH ~ "(" ~ EXPR ~ ")" ~ "{" ~ CASE_CLAUSES ~ "}"
}

CASE_CLAUSES = {
    CASE_CLAUSE* ~ DEFAULT_CLAUSE?
}

CASE_CLAUSE = {
    CASES ~ CASE_BODY
}

CASES = {
    (CASE ~ PRIMARY ~ ":")+
}

DEFAULT_CLAUSE = {
    DEFAULT ~ ":" ~ CASE_BODY
}

CASE_BODY = { STMTS }

BREAK_STMT = { BREAK ~ SCOLON }

CONTINUE_STMT = { CONTINUE ~ SCOLON }

GOTO_STMT = { GOTO ~ IDENTIFIER ~ SCOLON }

RETURN_STMT = { RETURN ~ EXPR? ~ SCOLON }

SLOT = {
    TYPE ~ NAME
}

DEF_VAR = { SLOT ~ SCOLON }

DEF_VARS = {
    STORAGE ~ TYPE ~ (NAME ~ (EQ ~ EXPR)?) ~ ("," ~ NAME ~ (EQ ~ EXPR)?)* ~ SCOLON
}

MEMBER_LIST = {
    "{" ~ (SLOT ~ ";")* ~ "}"
}

DEF_STRUCT = {
    STRUCT ~ NAME ~ MEMBER_LIST
}

DEF_UNION = {
    UNION ~ NAME ~ MEMBER_LIST
}

DEF_TYPE = {
    TYPEDEF ~ TYPEREF ~ IDENTIFIER ~ SCOLON
}

IMPORT_STMT = ${
    IMPORT ~ SP+ ~ IDENTIFIER ~ ("." ~ IDENTIFIER)* ~ ";"
}

DEF_CONST = {
   CONST ~ TYPE ~ NAME ~ (EQ ~ EXPR)?
}

STORAGE = { STATIC? }

DEF_FUNCTION = {
    STORAGE ~ TYPEREF ~ NAME ~ "(" ~ PARAMS ~ ")" ~ BLOCK
}

TOP_DEF = {
      DEF_UNION
    | DEF_STRUCT
    | DEF_CONST
    | DEF_FUNCTION
    | DEF_VARS
    | DEF_TYPE
}

FILE = { SOI ~ IMPORT_STMT* ~ TOP_DEF* ~ EOI }
